
plugins {
    id 'java-library'  // to build JVM libraries
    id 'maven-publish' // to publish artifacts to Maven repositories
}

group = 'com.github.capdevon'
version = '0.1.0'

// select one source-code (JDK) option
sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

ext {
    jmeVersion = '3.8.1-stable'
    recast4jVersion = '1.2.8'
    
    artifact = 'jme-navmesh-ai'
    baseName = "${artifact}-${version}" // for artifacts
}

gradle.projectsEvaluated {
    tasks.withType(JavaCompile).configureEach { // compile-time options:
        options.compilerArgs << '-Xdiags:verbose'
        options.compilerArgs << '-Xlint:unchecked'
        //options.deprecation = true
        options.encoding = 'UTF-8'
    }
    tasks.withType(JavaExec).tap {
        configureEach { // runtime options:
            args = []
            classpath sourceSets.main.runtimeClasspath
            //debug true
            //enableAssertions true
            //jvmArgs '-verbose:gc'
            //jvmArgs '-Xbatch'
            //jvmArgs '-Xms512m', '-Xmx512m'
            //jvmArgs '-XX:+PrintCompilation'
            //jvmArgs '-XX:+UseConcMarkSweepGC'
            jvmArgs '-XX:+UseG1GC', '-XX:MaxGCPauseMillis=10'
        }
    }
}

repositories {
    mavenCentral()
}

dependencies {
    // https://mvnrepository.com/artifact/org.apache.commons/commons-lang3
    implementation 'org.apache.commons:commons-lang3:3.18.0'

    // jMonkeyEngine
    implementation 'org.jmonkeyengine:jme3-core:' + jmeVersion
    implementation 'org.jmonkeyengine:jme3-desktop:' + jmeVersion
    implementation 'org.jmonkeyengine:jme3-effects:' + jmeVersion
    implementation 'org.jmonkeyengine:jme3-terrain:' + jmeVersion
    runtimeOnly    'org.jmonkeyengine:jme3-awt-dialogs:' + jmeVersion // JMonkeyEngine v3.6 only!
    
    // Physics
    implementation 'com.github.stephengold:Minie:9.0.1+big4'

    // select one version of LWJGL
    //runtimeOnly 'org.jmonkeyengine:jme3-lwjgl:' + jmeVersion  // LWJGL 2.x
    runtimeOnly 'org.jmonkeyengine:jme3-lwjgl3:' + jmeVersion // LWJGL 3.x

    runtimeOnly 'org.jmonkeyengine:jme3-jogg:' + jmeVersion
    runtimeOnly 'org.jmonkeyengine:jme3-plugins:' + jmeVersion
    //runtimeOnly 'org.jmonkeyengine:jme3-testdata:' + jmeVersion
    
    // recast4j
    implementation 'org.recast4j:parent:' + recast4jVersion
    implementation 'org.recast4j:detour-tile-cache:' + recast4jVersion
    implementation 'org.recast4j:detour-crowd:' + recast4jVersion
    implementation 'org.recast4j:detour-extras:' + recast4jVersion
    implementation 'org.recast4j:recast:' + recast4jVersion
    implementation 'org.recast4j:detour:' + recast4jVersion
    
    // Logging
    implementation 'org.slf4j:slf4j-api:1.7.32'
    implementation 'org.slf4j:slf4j-simple:1.7.32'
        
    // Libraries related to the Lemur GUI and Groovy:
    implementation 'com.simsilica:lemur:1.16.0'
    implementation 'com.simsilica:lemur-props:1.2.0'
    implementation 'com.simsilica:lemur-proto:1.13.0'
    runtimeOnly 'org.codehaus.groovy:groovy-jsr223:3.0.16'
}

// cleanup tasks
clean.dependsOn('cleanDLLs', 'cleanDyLibs', 'cleanLogs', 'cleanSOs')

tasks.register('cleanDLLs', Delete) {
    delete fileTree(dir: '.', include: '*.dll')
}
tasks.register('cleanDyLibs', Delete) {
    delete fileTree(dir: '.', include: '*.dylib')
}
tasks.register('cleanLogs', Delete) {
    delete fileTree(dir: '.', include: 'hs_err_pid*.log')
}
tasks.register('cleanSOs', Delete) {
    delete fileTree(dir: '.', include: '*.so')
}

jar {
    archiveBaseName = project.ext.baseName
    doLast {
        println "using Java ${JavaVersion.current()} (${System.getProperty("java.vendor")})"
    }
    manifest {
        attributes 'Created-By': "${JavaVersion.current()} (${System.getProperty("java.vendor")})"
    }
}
java.withJavadocJar()

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = project.ext.artifact
            groupId = "${group}"
            version = "${version}"
            from components.java
        }
    }
    repositories {
        mavenLocal()
    }
}
